@Data_API

metamodel Families {
	FamilyRegister {
		<>-families(0..*)->Family
	}
	
	Family {
		.id: EInt
		.name : EString
		
		<>-father(0..1)->FamilyMember
		<>-mother(0..1)->FamilyMember
		<>-sons(0..*)->FamilyMember
		<>-daughters(0..*)->FamilyMember
	}
	
	FamilyMember {
		.id: EInt
		.name : EString
	}
}

pattern FamilyPattern {
	register:FamilyRegister {
		-families->family
	}
	
	family:Family
}

abstract pattern FamilyMemberPattern {
	family:Family
	
	member:FamilyMember
}

pattern FatherPattern -> FamilyMemberPattern {
	family:Family {
		-father->$member
	}
}

pattern MotherPattern -> FamilyMemberPattern {
	family:Family {
		-mother->$member
	}
}

pattern DaughterPattern -> FamilyMemberPattern {
	family:Family {
		-daughters->$member
	}
}

pattern SonPattern -> FamilyMemberPattern {
	family:Family {
		-sons->$member
	}
}

rule createFamily {
	++ family: Family {
		.enamespace := <namespace>
		.name := <name>
		.id := <id>
		._cr_ := true
	}
}

rule createFamilyMember {
	++fm: FamilyMember {
		.id := <id>
		.enamespace := <namespace>
		.name := <name>
		._cr_ := true
	}
}

rule createRegisterFamilyEdge {
	register: FamilyRegister {
		++ -families->family {
			~_cr_ := true
		}
	}
	
	family: Family {
		.id : <id>
		.enamespace : <namespace>
		.name : <name>
	}
}

abstract rule createFamilyMemberEdge {
	family: Family {
		.id : <fid>
		.enamespace : <namespace>
		.name : <fname>
	}
	
	member: FamilyMember {
		.id : <id>
		.enamespace : <namespace>
		.name : <name>
	}
}

rule createFamilySonEdge -> createFamilyMemberEdge {
	family: Family {
		++ -sons->$member {
			~_cr_ := true
		}
	}
}

rule createFamilyDaughterEdge -> createFamilyMemberEdge {
	family: Family {
		++ -daughters->$member {
			~_cr_ := true
		}
	}
}
rule createFamilyMotherEdge -> createFamilyMemberEdge {
	family: Family {
		++ -mother->$member {
			~_cr_ := true
		}
	}
}

rule createFamilyFatherEdge -> createFamilyMemberEdge {
	family: Family {
		++ -father->$member {
			~_cr_ := true
		}
	}
}

rule changeNameOfFamily {
	family: Family {
		.id : <id>
		.name := <name>
	}	
}